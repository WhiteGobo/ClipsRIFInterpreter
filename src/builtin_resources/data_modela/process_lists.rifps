Document(
    Prefix(func <http://www.w3.org/2007/rif-builtin-function#>) 
    Prefix(pred <http://www.w3.org/2007/rif-builtin-predicate#>) 
    Prefix(owl <http://www.w3.org/2002/07/owl#>)
    Prefix(deffact <http://white.gobo/crifi/resource-manager/deffacts#>)
    Prefix(rema <http://white.gobo/crifi/resource-manager#>)
    Prefix(cs <http://clips.script/>)
    Prefix(crifi <http://white.gobo/>)
    Prefix(ex <http://example.com/>)
    Prefix(triplestordf <http://example.com/triples-to-rdf#>)
    Prefix(rif <http://www.w3.org/2007/rif#>)
    Prefix(entailment <http://www.w3.org/ns/entailment/>)
    Prefix(modelA <http://white.gobo/modelA#>)
    Group (
      Forall ?riflist ?rest (
        If And (
          ?riflist[rif:rest -> ?rest]
          Not (?rest # rif:Var)
        ) Then Do (
            Execute(crifi:set-graph-in-errorstate("rif:rest is only implemented for domain of type rif:Var"))
        )
      )
      Forall ?riflist ?itemlist (
        If And (
          ?riflist # ex:ExpressionGenerator
          ?riflist[rif:items -> ?itemlist]
          External(pred:is-list(?itemlist))
          Not( Exists ?rest (?riflist[rif:rest -> ?rest]))
        ) Then Do (
          (?function New())
          Assert( ?riflist[ex:as-expression -> ?function] )
          Assert( ?function[cs:function-name -> "<http://www.w3.org/2007/rif-builtin-function#make-list>"] )
          Assert( ?function[cs:function-args -> External(func:make-list())] )
          Assert( ?function["add-args-as-expression-from"^^rif:local -> ?itemlist] )
        )
      )
      (* ex:complete-add-args-as-expression-to-list *) Group (
        Forall ?itemlist ?function ?rest(
          If And (
            ?function["add-args-as-expression-from"^^rif:local -> ?itemlist]
            ?function[cs:function-args -> ?currentexprlist]
            External(pred:numeric-greater-than(External(func:count(?itemlist)) 0))
            ?firstarg[ex:as-expression -> ?firstasexpression]
            ?firstarg = External(func:get(?itemlist 0))
          ) Then Do (
            Modify(?function[cs:function-args -> External(func:append(?currentexprlist ?firstasexpression))])
            Modify(?function["add-args-as-expression-from"^^rif:local -> External(func:remove(?itemlist 0))])
          )
        )
        Forall ?itemlist ?function ?rest ?currentexprlist(
          If And (
            ?function["add-args-as-expression-from"^^rif:local -> ?itemlist]
            External(pred:is-list(?itemlist))
            0 = External(func:count(?itemlist))
          ) Then Do (
            Retract(?function["add-args-as-expression-from"^^rif:local -> ?itemlist])
          )
        )
      )

      Forall ?riflist (
        If And (
          ?riflist # modelA:ConstraintGenerator
          ?riflist # rif:List
        ) Then Do (
          (?listasvar New())
          Assert( ?riflist[ex:as-constraint -> ?listasvar] )
          Assert( ?listasvar # cs:Variable )
          Assert( ?listasvar # ex:Unknown-Variable )
          Assert( ?riflist # modelA:ConstraintNeedsAdditionalPatterns )
        )
      )
      Forall ?riflist ?formula ?assign ?listvar ?itemlist (
        If And (
          ?formula[modelA:binds-var -> ?assign]
          ?riflist # modelA:ConstraintGenerator
          ?riflist # rif:List
          ?riflist[rif:rest -> ?rest]
          ?riflist[ex:as-constraint -> ?listvar]
          ?rest[ex:as-constraint -> ?assign]
          ?riflist[rif:items -> ?itemlist]
        ) Then Do (
          (?function New())
          (?position New())
          Assert( ?assign[cs:var-as-const-expr -> ?function] )
          Assert( ?function[cs:function-name -> "<http://www.w3.org/2007/rif-builtin-function#sublist>"] )
          Assert( ?function[cs:function-args -> External(func:make-list(?listvar ?position))] )
          Assert( ?position[cs:string -> External(crifi:literal-to-clipsconstant(External(func:count(?itemlist))))])

        )
      )
      Forall ?riflist ?formula ?assign ?listvar ?itemlist (
        If And (
          ?formula[modelA:binds-var -> ?assign]
          ?riflist # modelA:ConstraintGenerator
          ?riflist # rif:List
          ?riflist[ex:as-constraint -> ?listvar]
          ?rifvar[ex:as-constraint -> ?assign]
          ?riflist[rif:items -> ?itemlist]
          External(pred:list-contains(?itemlist ?rifvar))
        ) Then Do (
          (?function New())
          (?position New())
          Assert( ?assign[cs:var-as-const-expr -> ?function] )
          Assert( ?function[cs:function-name -> "<http://www.w3.org/2007/rif-builtin-function#get>"] )
          Assert( ?function[cs:function-args -> External(func:make-list(?listvar ?position))] )
          Assert( ?position[cs:string -> External(crifi:literal-to-clipsconstant(External(func:index-of(?itemlist ?rifvar))))])

        )
      )

      Forall ?riflist ?itemlist ?rest (
        If And (
          ?riflist[rif:items -> ?itemlist]
          ?riflist # ex:ConstraintGenerator
          ?rifrule[ex:inherits-data -> ?riflist]
          ?rifrule[ex:as-defrule -> ?defrule]

          External(pred:is-list(?itemlist))
          ?riflist[rif:rest -> ?rest]
        ) Then Do (
          (?listasvar New())
          (?slotitems New())
          (?pattern New())
          Assert( ?riflist[ex:as-constraint -> ?listasvar] )
          Assert( ?riflist[ex:add-as-pattern -> ?defrule] )
          Assert( ?riflist # ex:ConstraintGenerator )

          Assert( ?listasvar # cs:Variable )
          Assert( ?listasvar # ex:Unknown-Variable )

          Assert( ?riflist[ex:as-lhs-pattern -> ?pattern] )
          Assert( ?pattern # cs:TemplatePatternCE )
          Assert( ?pattern[cs:deftemplate-name -> "AtomList"] )
          Assert( ?pattern[cs:slot -> External(func:make-list(?slotitems))] )
          Assert( ?slotitems[cs:slot-name -> "items"] )
          Assert( ?slotitems[cs:constraints -> External(func:make-list())] )

          Assert( ?slotitems["add-args-as-constraints-from"^^rif:local -> ?itemlist] )
          Assert( ?slotitems["rest"^^rif:local -> ?rest] )
        )
      )
        Forall ?itemlist ?slotitems ?firstarg (
          If And (
            ?slotitems["add-args-as-constraints-from"^^rif:local -> ?itemlist]
            ?slotitems[cs:constraints -> ?listconstraints]
            External(pred:is-list(?itemlist))
            External(pred:numeric-greater-than(External(func:count(?itemlist)) 0))
            ?firstarg[ex:as-constraint -> ?clipsconstraint]
            ?firstarg = External(func:get(?itemlist 0))
          ) Then Do (

            Modify(?slotitems[cs:constraints -> External(func:append(?listconstraints ?clipsconstraint))])
            Modify(?slotitems["add-args-as-constraints-from"^^rif:local -> External(func:remove(?itemlist 0))])
          )
        )
        Forall ?itemlist ?slotitems ?rest (
          If And (
            ?slotitems["add-args-as-constraints-from"^^rif:local -> ?itemlist]
            External(pred:is-list(?itemlist))
            0 = External(func:count(?itemlist))
            ?slotitems["rest"^^rif:local -> ?rest]
            ?rest # rif:Var
            ?slotitems[cs:constraints -> ?listconstraints]
          ) Then Do (
            (?clipsrestvar New())
            Assert( ?clipsrestvar # cs:MultifieldVariable )
            Assert( ?clipsrestvar # ex:Unknown-Variable )
            Assert( "combine-multifield-as-list"^^rif:local( ?rest ?clipsrestvar) )
            Retract( ?slotitems["rest"^^rif:local -> ?rest] )
            Modify( ?slotitems[cs:constraints -> External(func:append(?listconstraints ?clipsrestvar))] )
          )
        )

        Forall ?aslist ?multifieldvar ?rest ?clipsrestvar (
          If And (
            ?riflist[rif:rest -> ?rest]
            ?riflist[ex:add-as-pattern -> ?defrule]
            Not (?defrule[ex:bound-variable -> ?clipsrestvar])
            ?rest[ex:as-expression -> ?clipsrestvar]
            "combine-multifield-as-list"^^rif:local( ?rest ?multifieldvar )
            ?rest # rif:Var
          ) Then Do (
            (?function New())
            (?expand New())
            Assert( ?clipsrestvar[cs:var-as-const-expr -> ?function] )
            Assert( ?function[cs:function-name -> "<http://www.w3.org/2007/rif-builtin-function#make-list>"] )
            Assert( ?function[cs:function-args -> External(func:make-list(?expand))] )
            Assert( ?expand[cs:function-name -> "expand$"] )
            Assert( ?expand[cs:function-args -> External(func:make-list(?multifieldvar))] )

            Assert( ?riflist[ex:binds-variable -> ?clipsrestvar] )
          )
        )
        Group -1 (
          Forall ?defrule ?riflist ?pattern ?listasvar (
            If And (
              ?riflist # rif:List
              ?riflist[ex:add-as-pattern -> ?defrule]
              ?riflist[ex:as-lhs-pattern -> ?pattern]
              ?riflist[ex:as-constraint -> ?listasvar]
              Not( ?defrule[ex:bound-variable -> ?listasvar] )
              Not (Exists ?x ( And(
                ?pattern[ex:needs-variable -> ?x]
                Not( ?defrule[ex:bound-variable -> ?x] )
              )))
              Not( ?riflist[ex:used-as-pattern-in -> ?defrule] )
            ) Then Do (
              (?assignpattern New())
              (?oldpatternlist ?defrule[cs:conditional-element -> ?oldpatternlist])
              Assert( ?riflist[ex:used-as-pattern-in -> ?defrule] )
              Assert( ?assignpattern # cs:AssignedPatternCE )
              Assert( ?assignpattern[cs:pattern-ce -> ?pattern] )
              Assert( ?assignpattern[cs:fact-var-symbol -> ?listasvar] )
              Modify( ?defrule[cs:conditional-element -> External(func:append(?oldpatternlist ?assignpattern))] )
              Assert( ?defrule[ex:transfer-bound-variables-from -> ?pattern] )
            )
          )
          Forall ?defrule ?riflist ?pattern ?listasvar (
            If And (
              ?riflist # rif:List
              ?riflist[ex:add-as-pattern -> ?defrule]
              ?riflist[ex:as-lhs-pattern -> ?pattern]
              ?riflist[ex:as-constraint -> ?listasvar]
              ?defrule[ex:bound-variable -> ?listasvar]
              Not (Exists ?x ( And(
                ?pattern[ex:needs-variable -> ?x]
                Not( ?defrule[ex:bound-variable -> ?x] )
              )))
              Not( ?riflist[ex:used-as-pattern-in -> ?defrule] )
            ) Then Do (
              (?assignpattern New())
              (?tmplistasvar New())
              (?testvarpattern New())
              (?testvaraction New())
              (?oldpatternlist ?defrule[cs:conditional-element -> ?oldpatternlist])
              Assert( ?tmplistasvar # cs:Variable )
              Assert( ?tmplistasvar # ex:Unknown-Variable )

              Assert( ?testvarpattern # cs:TestCE )
              Assert(?testvarpattern[cs:function-call -> ?testvaraction])
              Assert(?testvaraction[cs:function-name -> "eq"])
              Assert(?testvaraction[cs:function-args -> External(func:make-list( ?listasvar ?tmplistasvar))])

              Assert( ?riflist[ex:used-as-pattern-in -> ?defrule] )
              Assert( ?assignpattern # cs:AssignedPatternCE )
              Assert( ?assignpattern[cs:pattern-ce -> ?pattern] )
              Assert( ?assignpattern[cs:fact-var-symbol -> ?tmplistasvar] )
              Modify( ?defrule[cs:conditional-element -> External(func:append(?oldpatternlist ?assignpattern ?testvarpattern))] )
              Assert( ?defrule[ex:transfer-bound-variables-from -> ?pattern] )
            )
          )
        )
  )
)
